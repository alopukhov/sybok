plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}

description = "Sybok junit engine"

dependencies {
    implementation ('org.junit.platform:junit-platform-engine') {
        version {
            require '[1.6,2.0['
            prefer '1.8.2'
        }
    }
    implementation ('org.codehaus.groovy:groovy') {
        version {
            prefer '3.0.9'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

def writeVersion = tasks.register("writeVersionInfo", WriteProperties) {
    outputFile = project.layout.buildDirectory.file("version-info.properties")
    property("group", project.group)
    property("artifact", project.name)
    property("version", project.version)
}

tasks.named("processResources", Copy).configure {
    dependsOn writeVersion
    with copySpec {
        from writeVersion
        into 'META-INF/sybok/'
    }
    with copySpec {
        from rootProject.layout.projectDirectory.file("LICENSE")
        into 'META-INF'
    }
}

publishing {
    repositories {
        maven {
            name = 'sonatype'
            final isReleaseVersion = !version.toString().toUpperCase().contains("SNAPSHOT")
            final releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            final snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Sybok engine'
                afterEvaluate {
                    description = project.description
                }
                url = 'https://github.com/alopukhov/sybok'
                scm {
                    connection = 'scm:git:git://github.com/alopukhov/sybok'
                    developerConnection = 'scm:git:git://github.com/alopukhov/sybok'
                    url = 'https://github.com/alopukhov/sybok'
                }
                developers {
                    developer {
                        id = 'alopukhov'
                        name = 'Andrei Lopukhov'
                    }
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}